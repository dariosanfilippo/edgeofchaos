// =============================================================================
// ========== auxiliaryEOC.lib =================================================
// =============================================================================
//
// Auxiliary functions library for testing, analysis, inspection, and debugging.
//
// The environment prefix is "au".
//
// List of functions:
//
//    dirac,
//    inspect,
//    step.
//
// Copyright (c) 2019-2020, Dario Sanfilippo <sanfilippo.dario at gmail dot com>
// All rights reserved.

declare name "Auxiliary Library";
declare author "Dario Sanfilippo";
declare copyright "Copyright (c) 2019-2020, Dario Sanfilippo <sanfilippo.dario
      at gmail dot com>";
declare version "2.0.0";
declare license "GPLv2.0";

au = library("auxiliary.lib");

// au.dirac; -------------------------------------------------------------------
//
// Dirac impulse: full-amplitude, 1-sample impulse.
// y[n] = 1 if n = 0; 0 otherwise.
//
// 0 inputs.
//
// 1 outputs: 
//    y[n].
//
dirac = 1 - 1';
// -----------------------------------------------------------------------------

// au.inspect(i, lower, upper, x[n]); ------------------------------------------
//
// Signal inspector: it displays the value of a signal at block-size rate.
//
// 1 inputs: 
//    x[n].
// 
// 1 outputs: 
//    x[n].
//
// 3 compile-time arguments: 
//    "i", integer identifier;
//    "lower", lower display limit; 
//    "upper", upper display limit.
//
inspect(i, lower, upper) = 
      _ <: _ ,
           vbargraph("sig_%i [style:numerical]", lower, upper) : attach;
// -----------------------------------------------------------------------------

// au.octaves(N); --------------------------------------------------------------
//
// It generates a list of N octaves lower-bound frequencies starting from 
// the base 10 ^ (.1 * 12), which is approximately 15.85 Hz.
//
octaves(N) = par(i, N, 10 ^ (.1 * (12 + i * 3)));
// -----------------------------------------------------------------------------

// au.octaves_cf(N); -----------------------------------------------------------
//
// It generates a list of N octaves centre frequencies starting from
// the base 10 ^ (.1 * 12), which is approximately 15.85 Hz.
//
octaves_cf(N) = par(i, N, 1.4142135624 * 10 ^ (.1 * (12 + i * 3)));
// -----------------------------------------------------------------------------

// au.octaves_thirds(N); -------------------------------------------------------
//
// It generates a list of N one-third-octave lower-bound frequencies starting 
// from the base 10 ^ (.1 * 12), which is approximately 15.85 Hz.
//
octaves_thirds(N) = par(i, N, 10 ^ (0.1 * (i + 12)));
// -----------------------------------------------------------------------------

// au.octaves_thirds_cf(N); ----------------------------------------------------
//
// It generates a list of N one-third-octave centre frequencies starting 
// from the base 10 ^ (.1 * 12), which is approximately 15.85 Hz.
//
octaves_thirds_cf(N) = par(i, N, 10 ^ 0.05 * 10 ^ (0.1 * (i + 12)));
// -----------------------------------------------------------------------------

// au.step(M); -----------------------------------------------------------------
//
// Step function: full-amplitude, M-sample step.
// y[n] = 1 if 0 <= n < M; 0 otherwise.
//
// 0 inputs.
//
// 1 outputs: 
//    Step().
//
// 1 compile-time arguments: 
//    "M", step size in samples.
//
step(M) = 1 <:  _ - (_ @ M);
// -----------------------------------------------------------------------------
