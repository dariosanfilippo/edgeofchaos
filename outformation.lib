// AUDIO PROCESSING LIBRARY

au = library("auxiliary.lib");
ba = library("basics.lib");
de = library("delays.lib");
d2 = library("delays2.lib");
f2 = library("filters2.lib");
ip = library("information.lib");
op = library("outformation.lib");
ma = library("maths.lib");
m2 = library("maths2.lib");
ro = library("routes.lib");
si = library("signals.lib");
st = library("stability.lib");

// real-time pitch-shifter with 6th-order pol. delay lines
pitch_shift(x, factor, frame, buff_size) = d2.del_pol(x, del1, buff_size)*w1, 
                                           d2.del_pol(x, del2, buff_size)*w2 :> _
with {
      frame_1 = frame : abs;
      rate =   1,
               frame_1 : m2.div;
      shift = (1-factor)*frame_1;
      offset = ba.if(  shift < 0,
                       -shift,
                       0);
      limit = buff_size*ma.SR : m2.round_pow2 : /(ma.SR); 
      ph1 = m2.ph(rate, 0);
      ph2 = ph1 : +(.5) : ma.decimal;
      w1 = m2.window_hann(ph1); 
      w2 = m2.window_hann(ph2);
      del1 = shift*ph1+offset : m2.wrap(_, 0, limit);
      del2 = shift*ph2+offset : m2.wrap(_, 0, limit); 
};

// real-time time-stretcher with 6th-order pol. delay lines
time_stretch(x, factor, frame, buff_size) =    d2.del_pol(x, del1, buff_size)*w1, 
                                               d2.del_pol(x, del2, buff_size)*w2 :> _
with {
      buff = buff_size*ma.SR : m2.round_pow2 : /(ma.SR);
      position = m2.ph((1-factor)/buff, 0)*buff;
      frame_1 = frame : abs;
      rate =   1,
               frame_1 : m2.div;
      ph1 = m2.ph(rate, 0);
      ph2 = ph1 : +(.5) : ma.decimal;
      w1 = m2.window_hann(ph1);
      w2 = m2.window_hann(ph2);
      del1 = position : ba.sAndH(ph1 : m2.diff : <(0));
      del2 = position : ba.sAndH(ph2 : m2.diff : <(0));
};

// table-based ZC granulator, i.e., discontinuities are handled through ZC detection
grains_zc(x, position, g_size) =    grains
                                               ~ _
with {
    s = 768000;
    l = g_size*ma.SR;
    p = position*ma.SR;
    input = x;
    rec_index = %(s) ~ +(1);
    grains(fb) =    int(s),
                    0.0,
                    int(rec_index),
                    input,
                    int(read_frame(fb) : %(s)) : rwtable
    with {
        sel_zc(x) = ba.if(  (m2.diff(x) : >(0)),
                            zc_up_index,
                            zc_down_index);
        frame(start) =  %(dur) ~ +(1)
        with {
            dur = zc_index(start+l)-start : max(2);
        };
        read_frame(out) = (frame <: _,
                                    (==(0),
                                    sel_zc(out) : ba.sAndH)) ~ (!, _) : +;
        zc_up =     (ip.zc(input),
                    (m2.diff(input) : >(0)) : &);
        zc_down =   (ip.zc(input),
                    (m2.diff(input) : <(0)) : &);
        zc_index(x) =   int(s),
                        0.0,
                        int(rec_index),
                        (   ip.zc(input),
                            rec_index : ba.sAndH),
                        int(x : %(s)) : rwtable;
        zc_up_index =   int(s),
                        0.0,
                        int(rec_index),
                        (   zc_up,
                            rec_index : ba.sAndH),
                        int(p : %(s)) : rwtable;
        zc_down_index = int(s),
                        0.0,
                        int(rec_index),
                        (   zc_down,
                            rec_index : ba.sAndH),
                        int(p : %(s)) : rwtable;
    };
};

// DL-based ZC granulator
grains_dl_zc(n, s) = par(i, n, grain) :> /(n)
with {
      size = s*ma.SR : m2.round_pow2;
      index = ba.period(size);
      grain(input, pitch, rate, pos) = (input,
                                       (   input,
                                               _,
                                            pitch,
                                            (rate : abs),
                                            pos : read_grain) : dl_pol)
                                       ~ _;
      read_grain(input, fb, pitch, rate, pos) = (  trigger,
                                                   index : ba.sAndH),
                                                (  trigger,
                                                   (   input,
                                                       pos,
                                                       fb : zc_index) : ba.sAndH) : - : +(shift) : m2.wrap(_, 0, size)
      with {
       zc_index(input, position, direction) = ba.if((m2.diff(direction) : >=(0)),
                                                    (  input,
                                                       p : zc_index_up),
                                                    (  input,
                                                       p : zc_index_down)) : +(1)
       with {
           p = position*ma.SR : m2.wrap(_, 0, size);
       };
       trigger =   check
                   ~ _
       with {
           check(ready) =  (fb : ip.zc),
                           (m2.line_reset(rate, ready) : >=(1)) : &;
       };
      shift =  (1,
               (   trigger,
                   pitch : ba.sAndH) : -)*(    1,
                                               (trigger,
                                               rate : ba.sAndH) : m2.div)*ma.SR*m2.line_reset( (trigger,
                                                                                               rate : ba.sAndH), trigger);
      };
      zc_index_up(input, pos) = (  (input : ip.zc),
                                   (m2.diff(input) : >(0)) : &),
                                index : ba.sAndH,
                                pos : dl;
      zc_index_down(input, pos) =  (   (input : ip.zc),
                                       (m2.diff(input) : <(0)) : &),
                                   index : ba.sAndH,
                                   pos : dl;
      dl_pol(in, del) = de.fdelayltv(6, size, del, in);
      dl(in, del) = in : de.delay(size, del);
};

// Feedback delay network reverb with non-transposing variable DL
rev_fdn_smo(n, max_size, it, size, fb_coeff, cf, in) = (summing : delays :filters : matrix : fb : limiters) 
                                                       ~ si.bus(n) :> /(n)
with {
      summing = par(i, n, +(in));
      delays = par(i, n,   (_ <:   _,
                                   (size : m2.prime_base_pow(i+1)))) : par(i, n,   _, 
                                                                                   _, 
                                                                                   max_size, 
                                                                                   it) : par(i, n, d2.del_smo);
      filters = par(i, n,  _,
                           cf : f2.lp1p);
      matrix = ro.hadamard(n);
      fb = par(i, n, *(fb_coeff));
      limiters = par(i, n, _,
                           1 : st.limiter);
};

// Feedback delay network reverb with variable DL
rev_fdn_pol(n, max_size, size, fb_coeff, cf, in) = (summing : delays :filters : matrix : fb : limiters) 
                                                   ~ si.bus(n) :> /(n)
with {
      summing = par(i, n, +(in));
      delays = par(i, n,   (_ <:   _,
                                   (size : m2.prime_base_pow(i+1)))) : par(i, n,   _, 
                                                                                   _, 
                                                                                   max_size) : par(i, n, d2.del_pol);
      filters = par(i, n,  _,
                           cf : f2.lp1p);
      matrix = ro.hadamard(n);
      fb = par(i, n, *(fb_coeff));
      limiters = par(i, n, _,
                           1 : st.limiter);
};
